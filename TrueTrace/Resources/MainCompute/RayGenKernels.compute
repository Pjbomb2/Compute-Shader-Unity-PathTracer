#include "../GlobalDefines.cginc"
#include "CommonData.cginc"
#ifndef DX11
    #pragma use_dxc
#endif

#ifdef HDRP
Texture2DArray<float2> MotionVectors;
Texture2DArray<float3> NormalTex;
Texture2DArray<float> Depth;
#else
Texture2D<float2> MotionVectors;
Texture2D<float3> NormalTex;
Texture2D<float> Depth;
#endif

#pragma kernel GenerateASVGF 

static const ColData InitCol = { 1,1,1,0,0,0,0,0,0,0,0,0,1,1,1,0};
const static RayHit tempray = CreateRayHit();

[numthreads(16, 16, 1)]
void GenerateASVGF(uint3 id : SV_DispatchThreadID) {

    if (id.x >= screen_width || id.y >= screen_height) return;
    const uint pixel_index = id.y * screen_width + id.x;
    RayData globray = {Rays[pixel_index].origin + NearPlane * Rays[pixel_index].direction, pixel_index, Rays[pixel_index].direction, 0, 0,0,asuint(FarPlane),0};
    GlobalRays[pixel_index] = globray;
    GlobalColors[pixel_index] = InitCol;

    #ifdef RadianceCache
        CacheBuffer[pixel_index].pathLength = 0;
        CacheBuffer[pixel_index].CurrentIlluminance = 0;
    #endif
}

#pragma kernel Generate
[numthreads(16, 16, 1)]
void Generate(uint3 id : SV_DispatchThreadID) {

    if (id.x >= screen_width || id.y >= screen_height) return;
    const uint pixel_index = id.y * screen_width + id.x;
    float2 jitter = ((random(0, pixel_index) - 0.5));
    float2 uv = float2((id.xy + jitter) / float2(screen_width, screen_height) * 2.0f - 1.0f);
    #ifdef RadianceCache
        CacheBuffer[pixel_index].pathLength = 0;
        CacheBuffer[pixel_index].CurrentIlluminance = 0;
    #endif
    SmallerRay ray = CreateCameraRay(uv, pixel_index);
    RayData globray = {ray.origin + NearPlane * ray.direction, pixel_index, ray.direction, 0, 0,0,asuint(FarPlane),0};
    GlobalRays[pixel_index] = globray;
    GlobalColors[pixel_index] = InitCol;
}



#pragma kernel GIReTraceKernel
[numthreads(16, 16, 1)]
void GIReTraceKernel(uint3 id : SV_DispatchThreadID, uint3 id2 : SV_GroupThreadID, uint3 id3 : SV_GroupID, uint id4 : SV_GroupIndex) {
    if (id.x >= screen_width || id.y >= screen_height) return;
    const uint pixel_index = id.y * screen_width + id.x;

    float2 uv = float2((id.xy) / float2(screen_width, screen_height));
    SmallerRay ray = CreateCameraRay(uv * 2.0f - 1.0f, pixel_index);
    RayData globray = {ray.origin + NearPlane * ray.direction, pixel_index, ray.direction, 0, 0,0,asuint(FarPlane),0};
    RandomNumsWrite[id.xy] = float4(frames_accumulated, pixel_index, 0, 0);

    if(curframe % ReSTIRGIUpdateRate == pixel_index % ReSTIRGIUpdateRate) {
        #ifdef HDRP
            float2 MotionVector = MotionVectors[int3(id.xy,0)].xy;
        #else
            float2 MotionVector = MotionVectors.SampleLevel(my_linear_clamp_sampler, uv, 0).xy;
        #endif
        int2 prevIndex = int2(floor(((float2(id.xy) + 0.5) * float2(rcp(screen_width), rcp(screen_height)) - MotionVector) * float2(screen_width, screen_height)));
        if (RandomNums[prevIndex].z == 0 && ReservoirA[prevIndex].z != 3 && prevIndex.x >= 0 && prevIndex.x < screen_width && prevIndex.y >= 0 && prevIndex.y < screen_height && !((PrevGlobalColorsA[prevIndex.x + prevIndex.y * screen_width].MetRoughIsSpec >> 22) & 0x1)) {
            RandomNumsWrite[id.xy] = float4(PrevGlobalColorsA[prevIndex.x + prevIndex.y * screen_width].throughput.yx, 1 + ReservoirA[prevIndex].z,  PrevGlobalColorsA[prevIndex.x + prevIndex.y * screen_width].throughput.z);
        }
    }
    
    #ifdef RadianceCache
        CacheBuffer[pixel_index].pathLength = 0;
        CacheBuffer[pixel_index].CurrentIlluminance = 0;
    #endif
    GlobalRays[pixel_index] = globray;
    GlobalColors[pixel_index] = InitCol;
}

bool DoPanorama;
float2 Segment;
#pragma kernel GeneratePanorama
[numthreads(16, 16, 1)]
void GeneratePanorama(uint3 id : SV_DispatchThreadID) {
    if (id.x >= screen_width || id.y >= screen_height) return;
    const uint pixel_index = id.y * screen_width + id.x;
    float2 jitter = ((random(0, pixel_index) - 0.5));
    float2 uv = float2((id.xy) / float2(screen_width, screen_height));
    #ifdef RadianceCache
        CacheBuffer[pixel_index].pathLength = 0;
        CacheBuffer[pixel_index].CurrentIlluminance = 0;
        CacheBuffer[pixel_index].throughput = EncodeRGB(float3(1,1,1));
    #endif
    SmallerRay ray;
    if(DoPanorama) {
        uv.y = 1.0f - uv.y;
        uv.x = (uv.x * (Segment.y - Segment.x)) + Segment.x;
        uv.x = 1.0f - uv.x;

        ray = CreateRay(mul(CamToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz + NearPlane * normalize(equirectUvToDirection(uv)), normalize(equirectUvToDirection(uv)));
    } else {
        uv.x = ((uv.x * (Segment.y - Segment.x)) + Segment.x);
        uv = (uv * 2.0f) - 1.0f;

        ray = CreateCameraRay(uv, pixel_index);
        ray.origin = ray.origin + NearPlane * ray.direction;
    }
    RayData globray = {ray.origin, pixel_index, ray.direction, 0, 0,0,asuint(FarPlane),0};
    GlobalRays[pixel_index] = globray;
    GlobalColors[pixel_index] = InitCol;
}



    groupshared bool ActiveData[256];
    groupshared uint TotalCount[8];
    #pragma kernel CacheResolve
    [numthreads(256, 1, 1)]
    void CacheResolve(uint3 id : SV_DispatchThreadID, uint3 GT : SV_GroupThreadID) {
    #ifdef RadianceCache
        HashEntriesBuffer[id.x].z = 0;
        uint2 HashValue = HashEntriesBuffer[id.x];
        bool Valid = !(id.x > CacheCapacity || (HashValue.x == 0 && HashValue.y == 0));
        int GroupIndex = floor(GT.x / (float)BucketCount);
        ActiveData[GT.x] = Valid;
        TotalCount[GroupIndex] = 0;
        GroupMemoryBarrierWithGroupSync();
        uint4 SummedVoxel = 0;
        [branch]if(ActiveData[GT.x]) {
            uint4 VoxelCurr = VoxelDataBufferA.Load4(id.x * 16);
            uint4 VoxelPrev = VoxelDataBufferB.Load4(id.x * 16);
            SummedVoxel = VoxelCurr + VoxelPrev;
            uint VoxelSampleCount = SummedVoxel.w & ((1u << 20) - 1);

            if ((VoxelSampleCount < MaxSampleCount) && any(CamPos - PrevCamPos) && (VoxelCurr.w != 0)) {
                uint2 SidelinedHashKey = GetReprojectedHash(HashValue);

                uint cacheEntry = 0xFFFFFFFF;
                if (HashGridFind(SidelinedHashKey, cacheEntry)) {
                    uint4 SidelinedVoxelPrev = VoxelDataBufferB.Load4(cacheEntry * 16);
                    uint SidelinedVoxelSampleCount = SidelinedVoxelPrev.w & ((1u << 20) - 1);
                    if (SidelinedVoxelSampleCount > 0) {
                        SummedVoxel.xyz += SidelinedVoxelPrev.xyz;
                        VoxelSampleCount += SidelinedVoxelSampleCount;
                    }
                }
            }

            if (VoxelSampleCount > MaxSampleCount) {
                SummedVoxel.xyz *= (float)MaxSampleCount / VoxelSampleCount;
                VoxelSampleCount = MaxSampleCount;
            }

            uint FrameCounter = (VoxelPrev.w >> 20) & ((1u << 12) - 1);
            SummedVoxel.w = VoxelSampleCount;

            if ((VoxelCurr.w & ((1u << 20) - 1)) == 0) {
                ++FrameCounter;
                SummedVoxel.w |= ((FrameCounter & ((1u << 12) - 1)) << 20);
            }

            if (FrameCounter > MaxSampleCount)
                SummedVoxel = 0;

            if(SummedVoxel.w == 0) ActiveData[GT.x] = false;
            else InterlockedAdd(TotalCount[GroupIndex], 1);
        }

        GroupMemoryBarrierWithGroupSync();
        if(!Valid) return;

        if(id.x % BucketCount >= TotalCount[GroupIndex]) {
            VoxelDataBufferA.Store4(id.x * 16, (uint4)0);
                
            uint Offset = 0;
            if(ActiveData[GT.x]) {
                uint Prefix = 0;
                for(int i = GroupIndex * BucketCount + TotalCount[GroupIndex] + 1; i < (GroupIndex + 1) * BucketCount; i++)
                    if(ActiveData[i]) Prefix++;

                uint Empty = 0;
                while(Empty < TotalCount[GroupIndex]) {
                    if(!ActiveData[Offset + GroupIndex * BucketCount]) {
                        if (Empty == Prefix) {
                            Offset += (id.x / BucketCount) * BucketCount;
                            VoxelDataBufferA.Store4(Offset * 16, SummedVoxel);
                            break;
                        }
                        Empty++;
                    }
                    Offset++;
                }
            }
            HashEntriesBuffer[id.x].z = Offset ? Offset : 0xFFFFFFFF;
        } else if(ActiveData[GT.x]) {
            VoxelDataBufferA.Store4(id.x * 16, SummedVoxel);
        } else {
            VoxelDataBufferA.Store4(id.x * 16, (uint4)0);
            
        }
    #endif
    }


    #pragma kernel ScreenCopy
    [numthreads(256, 1, 1)]
    void ScreenCopy(uint3 id : SV_DispatchThreadID) {
    #ifdef RadianceCache
        uint copyOffset = HashEntriesBuffer[id.x].z;
        if (copyOffset == 0 || id.x > CacheCapacity) return;
        if (copyOffset == 0xFFFFFFFF) HashEntriesBuffer[id.x] = (HashKeyValue)0;
        else if(copyOffset != 0) {
            HashKeyValue HashValue = uint3(HashEntriesBuffer[id.x].xy,0);
            HashEntriesBuffer[id.x] = (HashKeyValue)0;
            HashEntriesBuffer[copyOffset] = HashValue;
        }
        HashEntriesBuffer[id.x].z = 0;
    #endif
    }


    #pragma kernel ScreenClear
    [numthreads(256, 1, 1)]
    void ScreenClear(uint3 id : SV_DispatchThreadID) {
    #ifdef RadianceCache
        VoxelDataBufferA.Store4(id.x * 16, (uint4)0);
        HashEntriesBuffer[id.x] = uint3(HashEntriesBufferB[id.x].xy,0);
    #endif
    }