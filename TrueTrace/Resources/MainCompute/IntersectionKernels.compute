#include "../GlobalDefines.cginc"
#ifndef DX11
    #pragma use_dxc
    // #pragma enable_d3d11_debug_symbols
#endif
#include "CommonData.cginc"
#ifdef HardwareRT
    #include "UnityRayQuery.cginc"
    #pragma require inlineraytracing
    RaytracingAccelerationStructure myAccelerationStructure;
#endif

RWTexture2D<uint4> _PrimaryTriangleInfo;
#pragma kernel kernel_trace 

inline void IntersectTriangle(int mesh_id, const int tri_id, const SmallerRay ray, inout RayHit ray_hit, const int MatOffset, const float RandNum) {
    TrianglePos tri = triangle_get_positions(tri_id);
    TriangleUvs tri2 = triangle_get_positions2(tri_id);
    int MaterialIndex = (MatOffset + AggTris[tri_id].MatDat);

    float3 h = cross(ray.direction, tri.posedge2);
    float  a = dot(tri.posedge1, h);

    float  f = rcp(a);
    float3 s = ray.origin - tri.pos0;
    float  u = f * dot(s, h);

    if (u >= 0.0f && u <= 1.0f) {
        float3 q = cross(s, tri.posedge1);
        float  v = f * dot(ray.direction, q);

        if (v >= 0.0f && u + v <= 1.0f) {
            float t = f * dot(tri.posedge2, q);

            if (t > 0 && t < ray_hit.t) {
                //major slowdown
                #ifdef AdvancedAlphaMapped
                    if(_Materials[MaterialIndex].MatType == CutoutIndex) {
                        float2 BaseUv = tri2.pos0 * (1.0f - u - v) + tri2.posedge1 * u + tri2.posedge2 * v;
                        if(_Materials[MaterialIndex].AlphaTex.x > 0) 
                            if(SampleTexture(BaseUv, SampleAlpha, _Materials[MaterialIndex]).x < _Materials[MaterialIndex].AlphaCutoff) return;
                    }
                #endif
                #ifdef IgnoreGlassMain
                    if(_Materials[MaterialIndex].specTrans == 1) return;
                #endif
                #ifdef IgnoreBackfacing
                    if(_Materials[MaterialIndex].specTrans != 1 && dot(normalize(cross(normalize(tri.posedge1), normalize(tri.posedge2))), ray.direction) <= 0) return;
                #endif
                #ifdef FadeMapping
                    if(_Materials[MaterialIndex].MatType == FadeIndex) {
                        float2 BaseUv = tri2.pos0 * (1.0f - u - v) + tri2.posedge1 * u + tri2.posedge2 * v;
                        if(_Materials[MaterialIndex].AlphaTex.x > 0)
                            if(SampleTexture(BaseUv, SampleAlpha, _Materials[MaterialIndex]).x - _Materials[MaterialIndex].AlphaCutoff <= RandNum) return;
                    }
                #endif
                if(CurBounce == 0 && GetFlag(_Materials[MaterialIndex].Tag, Invisible)) return;
                ray_hit.t = t;
                ray_hit.u = u;
                ray_hit.v = v;
                ray_hit.mesh_id = mesh_id;
                ray_hit.triangle_id = tri_id;
            }
        }
    }
}


void IntersectBVH(int ray_index) {
    if(ray_index >= BufferSizes[CurBounce].tracerays) return;
    RayHit bestHit;
    SmallerRay ray;
    #ifndef HardwareRT
        int Reps = 0;
        uint2 stack[16];
        int stack_size = 0;
        uint2 current_group;
        uint oct_inv4;
        int tlas_stack_size = -1;
        SmallerRay ray2;
        int NodeOffset = 0;
        int TriOffset = 0;
        int MatOffset = 0;
        int mesh_id = -1;
    #endif

        [branch]if(CurBounce % 2 == 1) ray_index += screen_width * screen_height;
        RayData GlobalRay = GlobalRays[ray_index];
        bestHit = CreateRayHit();
        float RandNum = random(43, GlobalRay.PixelIndex).x;
        #ifdef HardwareRT
            const uint rayFlags = RAY_FLAG_FORCE_OPAQUE  ;
            UnityRayQuery<rayFlags> q;
            RayDesc myRay;
            myRay.Origin = GlobalRays[ray_index].origin;
            myRay.Direction = GlobalRays[ray_index].direction;
            ray.origin = myRay.Origin;
            ray.direction = myRay.Direction;
            myRay.TMin = 0;
            myRay.TMax = FarPlane;
            bestHit.t = FarPlane;
            while(true) {
               q.TraceRayInline(myAccelerationStructure, rayFlags, 0xff, myRay);
                while(q.Proceed()){}

                if(q.CommittedStatus() == COMMITTED_TRIANGLE_HIT) {
                    bestHit.t = q.CommittedRayT();
                    bestHit.u = q.CommittedTriangleBarycentrics().y;
                    bestHit.v = q.CommittedTriangleBarycentrics().x;
                    bestHit.mesh_id = uint(q.CommittedInstanceID() | (q.CommittedGeometryIndex() << 16));
                    bestHit.triangle_id = q.CommittedPrimitiveIndex();
                    #if defined(AdvancedAlphaMapped) || defined(IgnoreBackfacing) || defined(FadeMapping)
                        int2 Indexes = int2(bestHit.mesh_id & 0x0000FFFF, bestHit.mesh_id >> 16);
                        int triangleOffset = SubMeshOffsets[MeshOffsets[Indexes.x].x + Indexes.y];
                        int tri_id = bestHit.triangle_id + triangleOffset;
                        int mesh_id = MeshOffsets[Indexes.x].y;
                        int MaterialIndex = (_MeshData[mesh_id].MaterialOffset + AggTris[tri_id].MatDat);
                    #endif
                    #ifdef AdvancedAlphaMapped
                        if(CurBounce == 0 && GetFlag(_Materials[MaterialIndex].Tag, Invisible)) {myRay.TMin = bestHit.t + 0.001f; bestHit.t = FarPlane; continue;}
                        if(_Materials[MaterialIndex].MatType == CutoutIndex) {
                            float2 BaseUv = AggTris[tri_id].tex0 * (1.0f - bestHit.u - bestHit.v) + AggTris[tri_id].texedge1 * bestHit.u + AggTris[tri_id].texedge2 * bestHit.v;
                            if(_Materials[MaterialIndex].AlphaTex.x > 0)
                                if(SampleTexture(BaseUv, SampleAlpha, _Materials[MaterialIndex]).x < _Materials[MaterialIndex].AlphaCutoff) {myRay.TMin = bestHit.t + 0.001f; bestHit.t = FarPlane; continue;}
                        }
                    #endif
                    #ifdef IgnoreBackfacing
                        TrianglePos tri = triangle_get_positions(tri_id);
                        if(_Materials[MaterialIndex].specTrans != 1 && dot(normalize(cross(normalize(tri.posedge1), normalize(tri.posedge2))), mul((float3x3)_MeshData[mesh_id].W2L, ray.direction)) <= 0) {myRay.TMin = bestHit.t + 0.001f; bestHit.t = FarPlane; continue;}
                    #endif
                    #ifdef FadeMapping
                        if(_Materials[MaterialIndex].MatType == FadeIndex) {
                            float2 BaseUv = AggTris[tri_id].tex0 * (1.0f - bestHit.u - bestHit.v) + AggTris[tri_id].texedge1 * bestHit.u + AggTris[tri_id].texedge2 * bestHit.v;
                            if(_Materials[MaterialIndex].AlphaTex.x > 0)
                                if(SampleTexture(BaseUv, SampleAlpha, _Materials[MaterialIndex]).x - _Materials[MaterialIndex].AlphaCutoff <= RandNum) {myRay.TMin = bestHit.t + 0.001f; bestHit.t = FarPlane; continue;}
                        }
                    #endif
                }
                break;
            }


            int PixIndex = GlobalRay.PixelIndex;
            uint2 TempUv = uint2(PixIndex % screen_width, PixIndex / screen_width);
            int2 Indexes = int2(bestHit.mesh_id & 0x0000FFFF, bestHit.mesh_id >> 16);
            [branch]if(CurBounce == 0) {
                _PrimaryTriangleInfo[TempUv] = uint4(MeshOffsets[Indexes.x].y, bestHit.triangle_id + SubMeshOffsets[MeshOffsets[Indexes.x].x + Indexes.y] - _MeshData[MeshOffsets[Indexes.x].y].TriOffset, asuint(bestHit.u), asuint(bestHit.v));
            } else if(GetBounceData(GlobalColors[PixIndex].MetRoughIsSpec) == 0 || CurBounce + 1 == GetBounceData(GlobalColors[PixIndex].MetRoughIsSpec)) {
                _PrimaryTriangleInfo[TempUv] = uint4((UseReSTIRGI && bestHit.t != FarPlane) ? uint3(MeshOffsets[Indexes.x].y, bestHit.triangle_id + SubMeshOffsets[MeshOffsets[Indexes.x].x + Indexes.y] - _MeshData[MeshOffsets[Indexes.x].y].TriOffset, ((uint)(bestHit.u * 65535.0f) | ((uint)(bestHit.v * 65535.0f) << 16))) : ((UseASVGF || bestHit.t != FarPlane) ? asuint(ray.direction) : asuint(ray.direction * bestHit.t + ray.origin)), bestHit.t == FarPlane);
            }

            set(ray_index, bestHit);
        #else
            ray.origin = GlobalRay.origin;
            ray.direction = (GlobalRay.direction == 0 ? EPSILON : GlobalRay.direction);
            ray2 = ray;

            oct_inv4 = ray_get_octant_inv4(ray.direction);

            current_group.x = (uint)0;
            current_group.y = (uint)0x80000000;
            [loop]while (Reps < MaxTraversalSamples) {//Traverse Accelleration Structure(Compressed Wide Bounding Volume Hierarchy)          
                uint2 triangle_group;

                [branch]if (current_group.y & 0xff000000) {
                    uint child_index_offset = firstbithigh(current_group.y);

                    uint slot_index = (child_index_offset - 24) ^ (oct_inv4 & 0xff);
                    uint relative_index = countbits(current_group.y & ~(0xffffffff << slot_index));
                    uint child_node_index = current_group.x + relative_index;
                    const BVHNode8Data TempNode = cwbvh_nodes[child_node_index];

                    current_group.y &= ~(1 << child_index_offset);

                    if (current_group.y & 0xff000000) stack[stack_size++] = current_group;

                    uint hitmask = cwbvh_node_intersect(ray, oct_inv4, bestHit.t, TempNode);

                    current_group.y = (hitmask & 0xff000000) | ((TempNode.nodes[0].w >> 24) & 0xff);
                    triangle_group.y = (hitmask & 0x00ffffff);

                    current_group.x = (TempNode.nodes[1].x) + NodeOffset;
                    triangle_group.x = (TempNode.nodes[1].y) + TriOffset;

                    Reps++;
                } else {
                    triangle_group = current_group;
                    current_group = (uint)0;
                }

                if(triangle_group.y != 0) {
                    [branch]if(tlas_stack_size == -1) {
                        uint mesh_offset = firstbithigh(triangle_group.y);
                        triangle_group.y &= ~(1 << mesh_offset);
                        mesh_id = TLASBVH8Indices[triangle_group.x + mesh_offset];
                        NodeOffset = _MeshData[mesh_id].NodeOffset;
                        TriOffset = _MeshData[mesh_id].TriOffset;

                        if (triangle_group.y != 0) stack[stack_size++] = triangle_group;
                        if (current_group.y & 0xff000000) stack[stack_size++] = current_group;

                        tlas_stack_size = stack_size;

                        int root_index = (_MeshData[mesh_id].mesh_data_bvh_offsets & 0x7fffffff);

                        MatOffset = _MeshData[mesh_id].MaterialOffset;
                        ray.direction = (mul((float3x3)_MeshData[mesh_id].W2L, ray.direction));
                        ray.origin = (mul(_MeshData[mesh_id].W2L, float4(ray.origin, 1))).xyz;

                        oct_inv4 = ray_get_octant_inv4(ray.direction);

                        current_group.x = (uint)root_index;
                        current_group.y = (uint)0x80000000;
                    } else {
                        while (triangle_group.y != 0) {
                            uint triangle_index = firstbithigh(triangle_group.y);
                            triangle_group.y &= ~(1 << triangle_index);
                            IntersectTriangle(mesh_id, triangle_group.x + triangle_index, ray, bestHit, MatOffset, RandNum);//Intersect Mesh Triangles
                        }
                    }
                }
              
                if ((current_group.y & 0xff000000) == 0) {
                    if (stack_size == 0) {//thread has finished traversing
                        int PixIndex = GlobalRay.PixelIndex;
                        uint2 TempUv = uint2(PixIndex % screen_width, PixIndex / screen_width);
                        [branch]if(CurBounce == 0) {
                            _PrimaryTriangleInfo[TempUv] = uint4(bestHit.mesh_id, bestHit.triangle_id - _MeshData[bestHit.mesh_id].TriOffset, asuint(bestHit.u), asuint(bestHit.v));
                            #if DebugView == DVBVHView
                                _DebugTex[TempUv] = float4(Reps / 43.0f, Reps > 80, 0, 1);
                            #endif
                        } else if(GetBounceData(GlobalColors[PixIndex].MetRoughIsSpec) == 0 || CurBounce + 1 == GetBounceData(GlobalColors[PixIndex].MetRoughIsSpec)) {
                            _PrimaryTriangleInfo[TempUv] = uint4((UseReSTIRGI && bestHit.t != FarPlane) ? uint3(bestHit.mesh_id, bestHit.triangle_id - _MeshData[bestHit.mesh_id].TriOffset, ((uint)(bestHit.u * 65535.0f) | ((uint)(bestHit.v * 65535.0f) << 16))) : ((UseASVGF || bestHit.t != FarPlane) ? asuint(ray2.direction) : asuint(ray2.direction * bestHit.t + ray2.origin)), bestHit.t == FarPlane);
                        }
                        set(ray_index, bestHit);
                        return;
                    }

                    if (stack_size == tlas_stack_size) {
                        NodeOffset = 0;
                        TriOffset = 0;
                        tlas_stack_size = -1;
                        ray = ray2;
                        oct_inv4 = ray_get_octant_inv4(ray.direction);
                    }
                    current_group = stack[--stack_size];
                }
            }
    #endif
}


#ifdef DX11
[numthreads(64, 1, 1)]
#else
[numthreads(32, 1, 1)]
#endif
void kernel_trace(uint3 id : SV_DispatchThreadID) {//If I move the function contents into here, unity yells at me, so for now its just gonna continue to live in its function
    IntersectBVH(id.x);
}

#pragma kernel kernel_shadow

void IntersectBVHShadow(int ray_index) {
    SmallerRay ray;

    #ifndef HardwareRT
        uint2 stack[16];
        int stack_size = 0;
        uint2 current_group;
        int NodeOffset;
        int TriOffset;
        int MatOffset;
        uint oct_inv4;
        int tlas_stack_size;
        SmallerRay ray2;
        float max_distance;
        int Reps = 0;
    #endif

        int mesh_id = -1;

        if(ray_index >= BufferSizes[CurBounce].shadow_rays) return;

        bool hit = false;
        ShadowRayData ShadowRay = ShadowRaysBuffer[ray_index];
        float3 throughput = 1;
        #ifdef HardwareRT
            const uint rayFlags = RAY_FLAG_CULL_NON_OPAQUE;
            UnityRayQuery<rayFlags> q;
            RayDesc myRay;
            myRay.Origin = ShadowRaysBuffer[ray_index].origin;
            myRay.Direction = ShadowRaysBuffer[ray_index].direction;
            ray.origin = myRay.Origin;
            ray.direction = myRay.Direction;
            myRay.TMin = 0;
            myRay.TMax = abs(ShadowRaysBuffer[ray_index].t);
            while(true) {
                q.TraceRayInline(myAccelerationStructure, rayFlags, 0xff, myRay);
                while(q.Proceed()){}

                if(q.CommittedStatus() == COMMITTED_TRIANGLE_HIT) {

                    float u = q.CommittedTriangleBarycentrics().y;
                    float v = q.CommittedTriangleBarycentrics().x;
                    int2 Indexes = int2(q.CommittedInstanceID(), q.CommittedGeometryIndex());
                    int triangle_id = q.CommittedPrimitiveIndex();
                    #if defined(IgnoreBackfacing) || defined(AdvancedAlphaMapped) || defined(IgnoreGlassShadow)
                        int triangleOffset = SubMeshOffsets[MeshOffsets[Indexes.x].x + Indexes.y];
                        triangle_id += triangleOffset;
                        mesh_id = MeshOffsets[Indexes.x].y;
                        int MaterialIndex = (_MeshData[mesh_id].MaterialOffset + AggTris[triangle_id].MatDat);
                    #endif
                    #ifdef AdvancedAlphaMapped
                        if(_Materials[MaterialIndex].MatType == CutoutIndex) {
                            float2 BaseUv = AggTris[triangle_id].tex0 * (1.0f - u - v) + AggTris[triangle_id].texedge1 * u + AggTris[triangle_id].texedge2 * v;
                            if(_Materials[MaterialIndex].AlphaTex.x > 0)
                                if(SampleTexture(BaseUv, SampleAlpha, _Materials[MaterialIndex]).x < _Materials[MaterialIndex].AlphaCutoff) {myRay.TMin = q.CommittedRayT() + 0.001f; continue;}
                        }
                    #endif
                    #ifdef IgnoreBackfacing
                        TrianglePos tri = triangle_get_positions(triangle_id);
                        if(_Materials[MaterialIndex].specTrans != 1 && dot(normalize(cross(normalize(tri.posedge1), normalize(tri.posedge2))), mul((float3x3)_MeshData[mesh_id].W2L, ray.direction)) <= 0) {myRay.TMin = q.CommittedRayT() + 0.001f; continue;}
                    #endif
                    #ifdef IgnoreGlassShadow
                        if(_Materials[MaterialIndex].specTrans == 1) {
                            #ifdef StainedGlassShadows
                                float2 BaseUv = AggTris[triangle_id].tex0 * (1.0f - u - v) + AggTris[triangle_id].texedge1 * u + AggTris[triangle_id].texedge2 * v;
                                float3 MatCol = _Materials[MaterialIndex].surfaceColor;
                                if(_Materials[MaterialIndex].AlbedoTex.x > 0) MatCol *= SampleTexture(BaseUv, SampleAlbedo, _Materials[MaterialIndex]) / 3.0f;
                                MatCol = lerp(MatCol, _Materials[MaterialIndex].BlendColor, _Materials[MaterialIndex].BlendFactor);
                                throughput *= sqrt(exp(-CalculateExtinction2(1.0f - MatCol, _Materials[MaterialIndex].scatterDistance == 0.0f ? 1.0f : _Materials[MaterialIndex].scatterDistance)));
                            #endif
                            myRay.TMin = q.CommittedRayT() + 0.001f; 
                            continue;
                        }
                    #endif

                    ShadowRaysBuffer[ray_index].t = 0;
                } else {
                    if(!TerrainExists) {
                        uint PixelIndex = ShadowRaysBuffer[ray_index].PixelIndex;
                        ShadowRaysBuffer[ray_index].illumination *= throughput;
                        if(CurBounce == 0) NEEPosA[uint2(PixelIndex % screen_width, PixelIndex / screen_width)] = float4(ray.origin + ray.direction * abs(ShadowRaysBuffer[ray_index].t),NEEPosA[uint2(PixelIndex % screen_width, PixelIndex / screen_width)].w);
                            #ifdef RadianceCache
                                CacheBuffer[PixelIndex].CurrentIlluminance = EncodeRGB(DecodeRGB(CacheBuffer[PixelIndex].CurrentIlluminance) + unpackRGBE(ShadowRaysBuffer[ray_index].DiffuseIlluminance) * throughput);
                            #endif
                        if (ShadowRaysBuffer[ray_index].t >= 0) {
                            if (CurBounce == 0) {

                                GlobalColors[PixelIndex].Direct += ShadowRaysBuffer[ray_index].illumination;
                            } else {
                                #ifndef RadianceCache
                                    GlobalColors[PixelIndex].Indirect += ShadowRaysBuffer[ray_index].illumination;
                                #endif
                            }
                        } else {
                            #ifdef RadianceCache
                                if(CurBounce != 0 &&  (UseReSTIRGI || GetBounceData(GlobalColors[PixelIndex].MetRoughIsSpec) == CurBounce + 1)) GlobalColors[PixelIndex].Indirect += ShadowRaysBuffer[ray_index].illumination;
                            #else
                                if(CurBounce != 0 && (!UseReSTIRGI && GetBounceData(GlobalColors[PixelIndex].MetRoughIsSpec) == 0)) GlobalColors[PixelIndex].Indirect += ShadowRaysBuffer[ray_index].illumination;// * unpackRGBE(asuint(ShadowRay.DiffuseIlluminance));
                            #endif
                            else GlobalColors[PixelIndex].PrimaryNEERay = packRGBE(pow(unpackRGBE(GlobalColors[PixelIndex].PrimaryNEERay),2.2f) + pow(ShadowRaysBuffer[ray_index].illumination,rcp(2.2f)));
                        }
                    }
                }
                break;
            }
        #else
            max_distance = abs(ShadowRay.t);
            ray.origin = ShadowRay.origin;
            ray.direction = (ShadowRay.direction == 0 ? EPSILON : ShadowRay.direction);
            ray2 = ray;
            TriOffset = 0;
            MatOffset = 0;
            Reps = 0;
            NodeOffset = 0;

            oct_inv4 = ray_get_octant_inv4(ray.direction);

            current_group.x = (uint)0;
            current_group.y = (uint)0x80000000;
            tlas_stack_size = -1;
            [loop] while (Reps < MaxTraversalSamples) {//Traverse Accelleration Structure(Compressed Wide Bounding Volume Hierarchy)            
                uint2 triangle_group;
                [branch]if (current_group.y & 0xff000000) {
                   uint child_index_offset = firstbithigh(current_group.y);

                    uint slot_index = (child_index_offset - 24) ^ (oct_inv4 & 0xff);
                    uint relative_index = countbits(current_group.y & ~(0xffffffff << slot_index));
                    uint child_node_index = current_group.x + relative_index;
                    const BVHNode8Data TempNode = cwbvh_nodes[child_node_index];

                    current_group.y &= ~(1 << child_index_offset);

                    if (current_group.y & 0xff000000) stack[stack_size++] = current_group;


                    uint hitmask = cwbvh_node_intersect(ray, oct_inv4, max_distance, TempNode);

                    current_group.y = (hitmask & 0xff000000) | ((TempNode.nodes[0].w >> 24) & 0xff);
                    triangle_group.y = (hitmask & 0x00ffffff);

                    current_group.x = (TempNode.nodes[1].x) + NodeOffset;
                    triangle_group.x = (TempNode.nodes[1].y) + TriOffset;

                    Reps++;
                }
                else {
                    triangle_group = current_group;
                    current_group = (uint)0;
                }


                if(triangle_group.y != 0) {
                    [branch]if (tlas_stack_size == -1) {//Transfer from Top Level Accelleration Structure to Bottom Level Accelleration Structure
                        uint mesh_offset = firstbithigh(triangle_group.y);
                        triangle_group.y &= ~(1 << mesh_offset);

                        mesh_id = TLASBVH8Indices[triangle_group.x + mesh_offset];
                        NodeOffset = _MeshData[mesh_id].NodeOffset;
                        TriOffset = _MeshData[mesh_id].TriOffset;

                        if (triangle_group.y != 0) stack[stack_size++] = triangle_group;

                        if (current_group.y & 0xff000000) stack[stack_size++] = current_group;

                        tlas_stack_size = stack_size;

                        int root_index = (_MeshData[mesh_id].mesh_data_bvh_offsets & 0x7fffffff);

                        MatOffset = _MeshData[mesh_id].MaterialOffset;
                        ray.direction = (mul((float3x3)_MeshData[mesh_id].W2L, ray.direction)).xyz;
                        ray.origin = (mul(_MeshData[mesh_id].W2L, float4(ray.origin, 1))).xyz;

                        oct_inv4 = ray_get_octant_inv4(ray.direction);

                        current_group.x = (uint)root_index;
                        current_group.y = (uint)0x80000000;
                    } else {
                        while (triangle_group.y != 0) {    
                            uint triangle_index = firstbithigh(triangle_group.y);
                            triangle_group.y &= ~(1 << triangle_index);

                            if (triangle_intersect_shadow(triangle_group.x + triangle_index, ray, max_distance, mesh_id, throughput, MatOffset)) {
                                hit = true;
                                break;
                            }
                        }
                    }
                }

                if (hit) {
                    stack_size = 0;
                    current_group.y = 0;
                    ShadowRaysBuffer[ray_index].t = 0;
                    break;
                }

                if ((current_group.y & 0xff000000) == 0) {
                    if (stack_size == 0) {//thread has finished traversing
                        current_group.y = 0;
                        if(!TerrainExists) {
                            uint PixelIndex = ShadowRay.PixelIndex;
                            if(CurBounce == 0) NEEPosA[uint2(PixelIndex % screen_width, PixelIndex / screen_width)] = float4(ray2.origin + ray2.direction * abs(ShadowRaysBuffer[ray_index].t), 0);
                            #ifdef RadianceCache
                                CacheBuffer[PixelIndex].CurrentIlluminance = EncodeRGB(DecodeRGB(CacheBuffer[PixelIndex].CurrentIlluminance) + unpackRGBE(ShadowRay.DiffuseIlluminance) * throughput);// * abs(dot(i_octahedral_32(CacheBuffer[PixelIndex].Norm), ShadowRay.direction));
                            #endif
                            if (ShadowRay.t >= 0) {

                                if (CurBounce == 0) {
                                    GlobalColors[PixelIndex].Direct += ShadowRay.illumination * throughput;
                                } else {
                                    #if !defined(RadianceCache) || defined(ImprovedRadCacheSpecularResponse)
                                        GlobalColors[PixelIndex].Indirect += ShadowRay.illumination * throughput;
                                    #endif
                                }
                            } else {
                                #ifdef RadianceCache
                                    if(CurBounce != 0 &&  (UseReSTIRGI || GetBounceData(GlobalColors[PixelIndex].MetRoughIsSpec) == CurBounce + 1)) GlobalColors[PixelIndex].Indirect += ShadowRay.illumination * throughput;
                                #else
                                    if(CurBounce != 0 && (!UseReSTIRGI && GetBounceData(GlobalColors[PixelIndex].MetRoughIsSpec) == 0)) GlobalColors[PixelIndex].Indirect += ShadowRay.illumination * throughput;// * unpackRGBE(asuint(ShadowRay.DiffuseIlluminance));
                                #endif
                                else GlobalColors[PixelIndex].PrimaryNEERay = packRGBE(pow(unpackRGBE(GlobalColors[PixelIndex].PrimaryNEERay),2.2f) + pow(ShadowRay.illumination,rcp(2.2f)) * throughput);
                            }
                        } else {
                            ShadowRaysBuffer[ray_index].illumination *= throughput;
                            ShadowRaysBuffer[ray_index].DiffuseIlluminance = packRGBE(unpackRGBE(ShadowRaysBuffer[ray_index].DiffuseIlluminance) * throughput);
                        }
                        break;
                    }

                    if (stack_size == tlas_stack_size) {
                        NodeOffset = 0;
                        TriOffset = 0;
                        tlas_stack_size = -1;
                        ray = ray2;
                        oct_inv4 = ray_get_octant_inv4(ray.direction);
                    }
                    current_group = stack[--stack_size];
                }
            }
        #endif
}


#ifdef DX11
[numthreads(64, 1, 1)]
#else
[numthreads(32, 1, 1)]
#endif
void kernel_shadow(uint3 id : SV_DispatchThreadID) {//If I move the function contents into here, unity yells at me, so for now its just gonna continue to live in its function
    IntersectBVHShadow(id.x);
}



#pragma kernel kernel_shadow_heightmap


static float g = sin(atan(1.0f / 2.0f));

inline bool rayBoxIntersection(const float3 ray_orig, const float3 ray_dir, const float3 Min, const float3 Max, float tMax, inout float t0) {
    const float3 tmp_min = (Min - ray_orig) / ray_dir;
    const float3 tmp_max = (Max - ray_orig) / ray_dir;
    const float3 tmin = min(tmp_min, tmp_max);
    const float3 tmax = max(tmp_min, tmp_max);
    t0 = max(tmin.x, max(tmin.y, max(tmin.z, 0.025f))); // Usually ray_tmin = 0
    float t1 = min(tmax.x, min(tmax.y, min(tmax.z, tMax)));
    return (t0 <= t1);
}



bool IntersectHeightMapShadow(SmallerRay ray, float MaxDist) {
    float3 Origin = ray.origin;
    for (int i = 0; i < TerrainCount; i++) {
        const TerrainData Terrain = Terrains[i];
        float t0;
        if (rayBoxIntersection(Origin, ray.direction, Terrain.PositionOffset, Terrain.PositionOffset + float3(Terrain.TerrainDim.x, Terrain.TerrainDim.x, Terrain.TerrainDim.y), MaxDist, t0)) {
            ray.origin = Origin + ray.direction * (t0 + 0.001f) - Terrain.PositionOffset;
            float3 CurrentPos = ray.origin;
            int step_count = 0;
            float Dist = 0;
            while (step_count < 2000 && Dist < MaxDist && all(ray.origin + ray.direction * Dist < float3(Terrain.TerrainDim.x, 1000, Terrain.TerrainDim.y)) && all(CurrentPos > 0)) {
                CurrentPos = ray.origin + ray.direction * Dist;
                float3 b = float3(Terrain.TerrainDim.x, 0.01f, Terrain.TerrainDim.y);
                float3 q = (abs(CurrentPos) - b);
                q.x /= Terrain.TerrainDim.x;
                q.z /= Terrain.TerrainDim.y;
                float2 uv = float2(min(CurrentPos.x / Terrain.TerrainDim.x, 1), min(CurrentPos.z / Terrain.TerrainDim.y, 1)) * (Terrain.HeightMap.xy - Terrain.HeightMap.zw) + Terrain.HeightMap.zw;
                step_count++;
                float h = Heightmap.SampleLevel(sampler_trilinear_clamp, uv, 0).x;
                h *= Terrain.HeightScale * 2.0f;
                q.y -= h;
                q.y *= g;

                q = max(0, q);
                float dist = length(q);
                if (dist < 0.0001f) {
                    return true;
                }
                Dist += dist;

            }
        }
    }
    return false;

}

void IntersectShadowHeightmap(int ray_index) {
    if(ray_index >= BufferSizes[CurBounce].shadow_rays) return;
    int pixel_index = ShadowRaysBuffer[ray_index].PixelIndex;
    SmallerRay ray = CreateRay(ShadowRaysBuffer[ray_index].origin, ShadowRaysBuffer[ray_index].direction);
    ray.direction = (ray.direction == 0 ? EPSILON : ray.direction);
    bool HitHeightmap = TerrainExists;
    if(ShadowRaysBuffer[ray_index].t != 0) {
        if (TerrainExists) HitHeightmap = IntersectHeightMapShadow(ray, abs(ShadowRaysBuffer[ray_index].t));                  
        if(!HitHeightmap) {
            uint PixelIndex = ShadowRaysBuffer[ray_index].PixelIndex;
            if(CurBounce == 0) NEEPosA[uint2(PixelIndex % screen_width, PixelIndex / screen_width)] = float4(ray.origin + ray.direction * abs(ShadowRaysBuffer[ray_index].t), 0);
            // if(UseReSTIRGI && ReSTIRGIUpdateRate != 0) NEEPosA[uint2(PixelIndex % screen_width, PixelIndex / screen_width)] += float4(0,0,0,ShadowRay.DiffuseIlluminance);
            #ifdef RadianceCache
                CacheBuffer[PixelIndex].CurrentIlluminance = EncodeRGB(DecodeRGB(CacheBuffer[PixelIndex].CurrentIlluminance) + unpackRGBE(ShadowRaysBuffer[ray_index].DiffuseIlluminance));// * abs(dot(i_octahedral_32(CacheBuffer[PixelIndex].Norm), ShadowRay.direction));
            #endif
            if (ShadowRaysBuffer[ray_index].t >= 0) {

                if (CurBounce == 0) {
                    GlobalColors[PixelIndex].Direct += ShadowRaysBuffer[ray_index].illumination;
                } else {
                    #ifndef RadianceCache
                        GlobalColors[PixelIndex].Indirect += ShadowRaysBuffer[ray_index].illumination;
                    #endif
                }
            } else {
                if(CurBounce != 0 && UseReSTIRGI) GlobalColors[PixelIndex].Indirect += ShadowRaysBuffer[ray_index].illumination;// * unpackRGBE(asuint(ShadowRay.DiffuseIlluminance));
                else GlobalColors[PixelIndex].PrimaryNEERay = packRGBE(pow(unpackRGBE(GlobalColors[PixelIndex].PrimaryNEERay),2.2f) + pow(ShadowRaysBuffer[ray_index].illumination,rcp(2.2f)));
            }
        }
    }
}


#ifdef DX11
[numthreads(64, 1, 1)]
#else
[numthreads(32, 1, 1)]
#endif
void kernel_shadow_heightmap(uint3 id : SV_DispatchThreadID) {//If I move the function contents into here, unity yells at me, so for now its just gonna continue to live in its function
    IntersectShadowHeightmap(id.x);
}
#pragma kernel kernel_heightmap

bool GetDist(float3 CurrentPos, out float2 uv, out float dist, const TerrainData Terrain) {
    float3 b = float3(Terrain.TerrainDim.x, 0.01f, Terrain.TerrainDim.y);
    float3 q = (abs(CurrentPos) - b);
    q.x /= Terrain.TerrainDim.x;
    q.z /= Terrain.TerrainDim.y;
    uv = float2(min(CurrentPos.x / Terrain.TerrainDim.x, 1), min(CurrentPos.z / Terrain.TerrainDim.y, 1));
    float h = Heightmap.SampleLevel(sampler_trilinear_clamp, uv * (Terrain.HeightMap.xy - Terrain.HeightMap.zw) + Terrain.HeightMap.zw, 0).x;
    h *= Terrain.HeightScale * 2.0f;
    q.y -= h;
    q.y *= g;
    float b2 = q.y;
    q = max(0, q);
    dist = length(q);
    return b2 != abs(b2);
}

void IntersectHeightMap(int ray_index) {
    if(ray_index >= BufferSizes[CurBounce].tracerays) return;
    if(CurBounce % 2 == 1) ray_index += screen_width * screen_height;
    SmallerRay ray;
    SmallerRay ray2;

    RayHit bestHit;

    bool DidHit = false;
    float3 CurrentPos;
    int step_count = 0;
    ray.origin = GlobalRays[ray_index].origin;
    CurrentPos = ray.origin;
    ray.direction = GlobalRays[ray_index].direction;
    ray.direction = (ray.direction == 0 ? EPSILON : ray.direction);
    ray2 = ray;
    bestHit = get(ray_index);
    float dist = FarPlane;
    float2 uv;
    for (int i = 0; i < TerrainCount; i++) {
        float t0;
        float CurDist = 0;
        const TerrainData Terrain = Terrains[i];
        if (rayBoxIntersection(ray2.origin, ray.direction, Terrain.PositionOffset + 0.001f, Terrain.PositionOffset + float3(Terrain.TerrainDim.x, Terrain.TerrainDim.x, Terrain.TerrainDim.y) - 0.001f, bestHit.t, t0)) {
            CurDist = 0;
            dist = FarPlane;
            ray.origin = ray2.origin + ray.direction * max(t0 + 0.0001f, 0) - Terrain.PositionOffset;
            CurrentPos = ray.origin;
            float PrevDist = 0;
            step_count = 0;
            while (step_count < 2000 && CurDist < bestHit.t && all(ray.origin + ray.direction * CurDist < float3(Terrain.TerrainDim.x, 1000, Terrain.TerrainDim.y)) && all(ray.origin + ray.direction * CurDist > float3(0, 0, 0))) {
                CurrentPos = ray.origin + ray.direction * CurDist;
                step_count++;
                bool throwa = GetDist(CurrentPos, uv, dist, Terrain);
                if (dist < 0.0001f) {
                    if (throwa) {
                        CurDist -= PrevDist /= 2;
                        for (int i = 0; i < 10; i++) {
                            if (GetDist(ray.origin + ray.direction * CurDist, uv, dist, Terrain)) {
                                CurDist -= PrevDist /= 2;
                            }
                            else {
                                for (int i2 = 0; i2 < 10; i2++) {
                                    if (!GetDist(ray.origin + ray.direction * CurDist, uv, dist, Terrain)) {
                                        CurDist += PrevDist /= 2;
                                    }
                                    else {
                                        CurDist -= PrevDist;
                                        break;
                                    }
                                }
                                break;
                            }
                        }
                    }

                    int PixIndex = GlobalRays[ray_index].PixelIndex;
                    uint2 TempUv = uint2(PixIndex % screen_width, PixIndex / screen_width);

                    bestHit.u = uv.x;
                    bestHit.v = uv.y;
                    bestHit.t = CurDist + t0;
                    bestHit.triangle_id = i;
                    bestHit.mesh_id = 9999999;
                    [branch]if(CurBounce == 0) {
                        _PrimaryTriangleInfo[TempUv] = uint4(asuint(ray2.direction * bestHit.t + ray2.origin), 1);
                    } else if(CurBounce == 1) {
                        _PrimaryTriangleInfo[TempUv] = uint4(((!UseReSTIRGI) ? asuint(ray2.direction) : asuint(ray2.direction * bestHit.t + ray2.origin)), 1);
                    }
                    set(ray_index, bestHit);
                    break;
                }
                PrevDist = dist;
                CurDist += dist;

            }
        }
    }

}


#ifdef DX11
[numthreads(64, 1, 1)]
#else
[numthreads(32, 1, 1)]
#endif
void kernel_heightmap(uint3 id : SV_DispatchThreadID) {//If I move the function contents into here, unity yells at me, so for now its just gonna continue to live in its function

    IntersectHeightMap(id.x);
}





